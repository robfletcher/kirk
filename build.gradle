buildscript {
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  configurations.maybeCreate("pitest")

  dependencies {
    classpath "info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0"
    pitest "org.pitest:pitest-junit5-plugin:0.7"
  }
}

plugins {
  id "nebula.release" version "6.3.5"
  id "com.ferranpons.twitterplugin" version "1.0.2"
  id "nebula.kotlin" version "1.2.70" apply false
  id "org.jetbrains.dokka" version "0.9.17" apply false
  id "org.jmailen.kotlinter" version "1.17.0" apply false
  id "nebula.maven-publish" version "9.0.1" apply false
  id "nebula.source-jar" version "9.0.1" apply false
  id "nebula.nebula-bintray-publishing" version "4.0.1" apply false
}
apply from: "$rootDir/gradle/announce.gradle"

allprojects {
  group = "io.strikt"

  configurations.all {
    resolutionStrategy {
      componentSelection {
        all { ComponentSelection selection ->
          boolean rejected = ["alpha", "beta", "rc", "cr", "m", "pr"].any { qualifier ->
            selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
          }
          if (rejected) {
            selection.reject("Release candidate")
          }
        }
      }
    }
  }
}

subprojects {
  repositories {
    jcenter()
  }
}

