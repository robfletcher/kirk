plugins {
  id "java-library"
  id "nebula.release"
  id "nebula.kotlin"
  id "org.jetbrains.dokka"
  id "org.jmailen.kotlinter"
  id "nebula.maven-publish"
  id "nebula.nebula-bintray-publishing"
  id "com.google.protobuf" version "0.8.6"
}
apply from: "$rootDir/gradle/kotlin.gradle"
apply from: "$rootDir/gradle/jupiter.gradle"
apply from: "$rootDir/gradle/published.gradle"

dependencies {
  api project(":strikt-core")
  api "com.google.protobuf:protobuf-java:+"
}

dokka {
  externalDocumentationLink {
    url = new URL("https://developers.google.com/protocol-buffers/docs/reference/java/")
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:+"
  }
  generateProtoTasks {
    ofSourceSet("test")
  }
}

// This seems to be necessary just to get IntelliJ to notice the proto sources.
// It works fine from the terminal without this. Also IntelliJ picks up main
// protos just fine.
sourceSets {
  test {
    kotlin {
      srcDirs "src/test/kotlin", "$buildDir/generated/source/proto/test/java"
    }
  }
}
